set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

MBIN_COMPILER_VERSION := "v6.13.0-pre1"
HGPAK_TOOL_VERSION    := "1.0.0"

default: clean setup extract-mappings extract-rewards

# ---------------------------------------------------------------------------
# ðŸ§¹  CLEANUP
# ---------------------------------------------------------------------------
clean:
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸ§¹ CLEANUP"
    rm -rf ./out/*
# ---------------------------------------------------------------------------
# âš™ï¸  SETUP & TOOL DOWNLOADS
# ---------------------------------------------------------------------------
setup: download-tools

download-tools:
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "âš™ï¸ TOOL DOWNLOADS"

    @gum spin --spinner dot --title "Downloading MBINCompiler ({{MBIN_COMPILER_VERSION}})..." -- curl -sSL https://github.com/monkeyman192/MBINCompiler/releases/download/{{MBIN_COMPILER_VERSION}}/MBINCompiler-linux-dotnet6 -o ./out/mbincompiler
    @gum style --foreground 10 "âœ… Downloaded MBINCompiler ({{MBIN_COMPILER_VERSION}})."
    @chmod +x ./out/mbincompiler

    @gum spin --spinner dot --title "Downloading HGPAKtool ({{HGPAK_TOOL_VERSION}})..." -- curl -sSL https://github.com/monkeyman192/HGPAKtool/releases/download/{{HGPAK_TOOL_VERSION}}/hgpaktool-x86_64-unknown-linux.gz -o ./out/hgpaktool.gz
    @gum style --foreground 10 "âœ… Downloaded HGPAKtool ({{HGPAK_TOOL_VERSION}})."
    @gunzip -c ./out/hgpaktool.gz > ./out/hgpaktool
    @rm ./out/hgpaktool.gz
    @chmod +x ./out/hgpaktool

# ---------------------------------------------------------------------------
# ðŸ—ºï¸  DATA EXTRACTION
# ---------------------------------------------------------------------------
extract-mappings:
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸ“œ mapping.json"
    @gum spin --spinner dot --title "Downloading mapping.json..." -- curl -sSL "https://github.com/monkeyman192/MBINCompiler/releases/download/{{MBIN_COMPILER_VERSION}}/mapping.json" -o ./out/mapping.json
    @jq '.Mapping | map({(.Key): .Value}) | add' ./out/mapping.json > ../src/data/mapping.json
    @npx prettier --write ../src/data/mapping.json --config ../.prettierrc > /dev/null
    @gum style --foreground 10 "âœ… mapping.json created."

extract-metadata-hgpak:
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸ“¦ HGPAK MetadataEtc EXTRACTION"
    @gum spin --spinner dot --title "Extracting..." -- ./out/hgpaktool ./PCBANKS/NMSARC.MetadataEtc.pak -O ./out/EXTRACTED
    @gum style --foreground 10 "âœ… NMSARC.MetadataEtc.pak extracted."

extract-precache-hgpak:
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸ“¦ HGPAK Precache EXTRACTION"
    @gum spin --spinner dot --title "Extracting..." -- ./out/hgpaktool ./PCBANKS/NMSARC.Precache.pak -O ./out/EXTRACTED
    @gum style --foreground 10 "âœ… NMSARC.Precache.pak extracted."

extract-locale: extract-metadata-hgpak
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸ“œ translations.json"
    @for f in ./out/EXTRACTED/language/*_usenglish.mbin; do \
        gum spin --spinner dot --title "Decoding $f" -- ./out/mbincompiler -y -d ./out/EXTRACTED/language/ "$f" > /dev/null; \
    done
    @gum spin --spinner dot --title "Extracting translations" -- python ./src/extract_translations.py
    @gum style --foreground 10 "âœ… translations.json ready."

extract-items: extract-precache-hgpak extract-locale
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸ“œ item_to_name_mappings.json"
    @gum spin --spinner dot --title "Decoding nms_reality_gcproducttable.mbin" -- ./out/mbincompiler -y -d ./out/ "./out/EXTRACTED/metadata/reality/tables/nms_reality_gcproducttable.mbin" > /dev/null;
    @gum spin --spinner dot --title "Decoding nms_basepartproducts.mbin" -- ./out/mbincompiler -y -d ./out/ "./out/EXTRACTED/metadata/reality/tables/nms_basepartproducts.mbin" > /dev/null;
    @python ./src/extract_item_names.py
    @gum style --foreground 10 "âœ… item_to_name_mappings.json created."

# ---------------------------------------------------------------------------
# ðŸŽ  REWARDS
# ---------------------------------------------------------------------------
extract-rewards: extract-season-rewards extract-twitch-rewards extract-platform-rewards

extract-season-rewards: extract-items
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸŽ rewards/season.json"
    @gum spin --spinner dot --title "Decoding unlockableseasonrewards.mbin" -- ./out/mbincompiler -y -d ./out/ "./out/EXTRACTED/metadata/reality/tables/unlockableseasonrewards.mbin" > /dev/null
    @gum spin --spinner dot --title "Extracting rewards" -- python ./src/extract_season_rewards.py
    @cp ./out/season_rewards.json ../src/data/rewards/season.json
    @npx prettier --write ../src/data/rewards/season.json --config ../.prettierrc > /dev/null
    @gum style --foreground 10 "âœ… rewards/season.json created."

extract-twitch-rewards: extract-items
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸŽ rewards/twitch.json"
    @gum spin --spinner dot --title "Decoding unlockabletwitchrewards.mbin" -- ./out/mbincompiler -y -d ./out/ "./out/EXTRACTED/metadata/reality/tables/unlockabletwitchrewards.mbin" > /dev/null
    @gum spin --spinner dot --title "Extracting rewards" -- python ./src/extract_twitch_rewards.py
    @cp ./out/twitch_rewards.json ../src/data/rewards/twitch.json
    @npx prettier --write ../src/data/rewards/twitch.json --config ../.prettierrc > /dev/null
    @gum style --foreground 10 "âœ… rewards/twitch.json created."

extract-platform-rewards: extract-items
    @gum style --border rounded --border-foreground 45 --margin "1 0 0 0" --padding "0 3" --width 80 --align left "ðŸŽ rewards/platform.json"
    @gum spin --spinner dot --title "Decoding unlockableplatformrewards.mbin" -- ./out/mbincompiler -y -d ./out/ "./out/EXTRACTED/metadata/reality/tables/unlockableplatformrewards.mbin" > /dev/null
    @gum spin --spinner dot --title "Extracting rewards" -- python ./src/extract_platform_rewards.py
    @cp ./out/platform_rewards.json ../src/data/rewards/platform.json
    @npx prettier --write ../src/data/rewards/platform.json --config ../.prettierrc > /dev/null
    @gum style --foreground 10 "âœ… rewards/platform.json created."
